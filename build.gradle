buildscript {
    ext.kotlin_version = '1.2.0-rc-39'

    repositories {
        jcenter()
        maven {
            url 'http://dl.bintray.com/kotlin/kotlin-eap-1.2'
        }
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.7.3'
    }
}

allprojects {
    apply plugin: "idea"

    group = 'actor.proto'
    version = '0.8-SNAPSHOT'
}

subprojects {
    apply plugin: 'kotlin'

    repositories {
        jcenter()
        maven {
            url 'http://dl.bintray.com/kotlin/kotlin-eap-1.2'
        }
    }

    dependencies {
        compile group: 'org.jetbrains.kotlin', name: 'kotlin-stdlib', version: kotlin_version
        compile group: 'org.jetbrains.kotlin', name: 'kotlin-stdlib-jre8', version: kotlin_version
        compile 'org.jetbrains.kotlinx:kotlinx-coroutines-core:0.17'
        compile 'org.jetbrains.kotlinx:kotlinx-coroutines-jdk8:0.17'

        testCompile group: 'org.jetbrains.kotlin', name: 'kotlin-test-junit', version: kotlin_version
        testCompile group: 'junit', name: 'junit', version:'4.12'
    }

    kotlin {
        experimental {
            coroutines 'enable'
        }
    }

    compileKotlin {
        kotlinOptions {
            jvmTarget = "1.8"
        }
    }
}

configure(subprojects - project(':examples')) {
    apply from: "$rootDir/gradle/publishing.gradle"
    apply plugin: 'jacoco'

    jacocoTestReport {
        reports {
            xml.enabled = true
            html.enabled = true
        }
    }

    check.dependsOn jacocoTestReport
}

task wrapper(type: Wrapper) {
    gradleVersion = '4.3.1'
}
